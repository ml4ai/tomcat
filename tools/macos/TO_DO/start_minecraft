#!/usr/bin/osascript

-- Get current path
set _current_path to ""
tell application "Finder"
        set _current_path to POSIX path of (container of (path to me) as string)
end tell

-- Load configuration library
property _configuration_library_path : ""
set _configuration_library_path to _current_path & "compiled/config_library.scpt"
set configLibrary to (load script POSIX file _configuration_library_path)

-- Load spaces library 
property _spaces_library_path : ""
set _spaces_library_path to _current_path & "compiled/spaces_library.scpt"
set spacesLibrary to (load script POSIX file _spaces_library_path)

-- Get Tomcat directory
set TOMCAT_DIRECTORY_PATH to ""
tell configLibrary
        initialize()
        set TOMCAT_DIRECTORY_PATH to get_config_value("TOMCAT_DIRECTORY_PATH")
end tell

-- Get width and heigh of display
tell application "Finder"
	set _b to bounds of window of desktop
	set _width to item 3 of _b
	set _height to item 4 of _b
end tell

-- Get coordinates for ClientMap
set _x_client to 0
set _y_client to 0
set _width_client to _width/3
set _height_client to _height

-- Get coordinates for Minecraft
set _x_minecraft to _width_client
set _y_minecraft to 0
set _width_minecraft to (_width/3)*2
set _height_minecraft to _height
 
-- Switch to Minecraft space
tell spacesLibrary
	go_to_space(1)
end tell

-- Launch Minecraft
do shell script TOMCAT_DIRECTORY_PATH & "/tools/launch_minecraft > /dev/null 2>&1 &"
delay 45
tell application "System Events" to tell process "java"
	tell window 1
		set size to {_width_minecraft, _height_minecraft}
		set position to {_x_minecraft, _y_minecraft} 
	end tell
end tell

-- Launch Clientmap
tell application "Firefox"
	activate
	open location "http://localhost:9000/ClientMap"
	set bounds of front window to {_x_client, _y_client, _width_client, _height_client}
end tell
